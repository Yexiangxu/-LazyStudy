apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'//用于编译Android代码
apply plugin: 'kotlin-kapt'
//apply plugin: 'android-aspectjx'
class HenCoderPlugin implements Plugin<Project> {

    @Override
    void apply(Project target) {
        def extension=target.extensions.create('hencoder',HenCoderExtension)
        println "Hello ${extension.name}"
    }
}
class  HenCoderExtension{
    def name='lazyxu'
}

apply plugin: HenCoderPlugin


android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.compileSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        resConfigs "zh"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        ndk {//选择要添加的对应cpu类型的.so库，armeabi-v7a、arm64-v8a这两个占据99%
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
    }
    kapt {
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }
    buildFeatures {
        dataBinding = true // for data binding
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    //签名
    signingConfigs {
        Properties props = new Properties()
        def propFile = rootProject.file('keystore.properties')
        if (propFile.canRead()) {
            props.load(new FileInputStream(propFile))
        }
        release {//发布版本的签名配置
            storeFile file(props['KEYSTORE_FILE'])
            keyAlias props['KEY_ALIAS']
            storePassword props['KEYSTORE_PWD']
            keyPassword props['KEY_PWD']
        }
        debug {//调试版本的签名配置
            storeFile file(props['DEBUG_KEYSTORE_FILE'])
            keyAlias props['DEBUG_KEY_ALIAS']
            storePassword props['DEBUG_KEYSTORE_PWD']
            keyPassword props['DEBUG_KEY_PWD']
        }
    }

    buildTypes {//buildTypes针对开发测试

        debug {
            minifyEnabled false //代码混淆
            shrinkResources false  //移除无用的resource文件
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false //代码混淆
            shrinkResources false  //移除无用的resource文件
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'paying'
    productFlavors {//免费版、付费版 productFlavors针对用户
        free {
            dimension "paying"
        }
        paid {
            dimension "paying"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    testImplementation 'junit:junit:4.13'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0'
    implementation 'android.arch.navigation:navigation-ui:1.0.0'
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0"
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"


    if (rootProject.ext.isModule) {
        implementation project(':base_common')

    } else {
        implementation project(':film')
        implementation project(':user')
    }
    implementation 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
//    implementation 'com.github.hackware1993:MagicIndicator:1.7.0'

    //提供协程的基础API
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"
    //提供一个Android主线程（类似于在使用RxJava时使用rxandroid）
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"




//    compileOnly 'com.android.tools.build:gradle:3.0.1'
}
